BACKUP ~d0tweak/backup~
AUTHOR ~angel@pearlgates.net~
VERSION ~23~

ALWAYS
  COPY_EXISTING_REGEXP GLOB ~^anomen[0-9]*\.cre~ ~override~
                            ~^imoen[0-9]*\.cre~  ~override~
                            ~^mazzy[0-9]*\.cre~  ~override~
    READ_LONG 0x244 "current_kit_value"
    PATCH_IF ("%current_kit_value%" = 0x00000000)
    BEGIN
      WRITE_LONG 0x244 0x00004000
    END
  BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING_REGEXP GLOB ~^haer[0-9]*\.cre~ ~override~
    READ_LONG 0x244 "current_kit_value"
    PATCH_IF ("%current_kit_value%" = 0x00000020)
    BEGIN
      WRITE_LONG 0x244 0x400d0000
    END
  BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING_REGEXP GLOB ~^korgan[0-9]*\.cre~ ~override~
    READ_LONG 0x244 "current_kit_value"
    PATCH_IF ("%current_kit_value%" = 0x00010000) BEGIN
      WRITE_LONG 0x244 0x40010000
    END
  BUT_ONLY_IF_IT_CHANGES

  COPY_EXISTING ~spell.ids~ ~override~
    REPLACE_TEXTUALLY ~PSIONIC _SUPERIOR_INVISIBILITY~ ~PSIONIC_SUPERIOR_INVISIBILITY~
  BUT_ONLY_IF_IT_CHANGES

  //Kit fixes (CamDawg & the bigg & The Imps tweak)
  COPY_EXISTING ~kit.ids~ ~override~
    REPLACE_TEXTUALLY ~0x0000 BARBARIAN~           ~0x40000000 BARBARIAN~
    REPLACE_TEXTUALLY ~0x0000 WILDMAGE~            ~0x80000000 WILDMAGE~
  BUT_ONLY_IF_IT_CHANGES

  CLEAR_INLINED

  APPEND ~kit.ids~ ~0x00004000 TRUECLASS~
  UNLESS ~\bTRUECLASS\b~

  APPEND ~kit.ids~ ~0x40000000 BARBARIAN~
  UNLESS ~\bBARBARIAN\b~

  APPEND ~kit.ids~ ~0x80000000 WILDMAGE~
  UNLESS ~\bWILDMAGE\b~

  APPEND ~state.ids~ ~0x00000FC0 STATE_REALLY_DEAD~
  UNLESS ~STATE_REALLY_DEAD~

  APPEND ~ACTION.IDS~
    ~31 SpellRES(S:RES*,O:Target*)~
  UNLESS ~31 SpellRES(S:RES*,O:Target*)~

  APPEND ~ACTION.IDS~
    ~95 SpellPointRES(S:RES*,P:Target*)~
  UNLESS ~95 SpellPointRES(S:RES*,P:Target*)~

  APPEND ~ACTION.IDS~
    ~113 ForceSpellRES(S:RES*,O:Target)~
  UNLESS ~113 ForceSpellRES(S:RES*,O:Target)~

  APPEND ~ACTION.IDS~
    ~114 ForceSpellPointRES(S:RES*,P:Target)~
  UNLESS ~114 ForceSpellPointRES(S:RES*,P:Target)~

  APPEND ~ACTION.IDS~
    ~160 ApplySpellRES(S:RES*,O:Target)~
  UNLESS ~160 ApplySpellRES(S:RES*,O:Target)~

  APPEND ~ACTION.IDS~
    ~181 ReallyForceSpellRES(S:RES*,O:Target)~
  UNLESS ~181 ReallyForceSpellRES(S:RES*,O:Target)~

  APPEND ~ACTION.IDS~
    ~191 SpellNoDecRES(S:RES*,O:Target*)~
  UNLESS ~191 SpellNoDecRES(S:RES*,O:Target*)~

  APPEND ~ACTION.IDS~
    ~192 SpellPointNoDecRES(S:RES*,P:Target*)~
  UNLESS ~192 SpellPointNoDecRES(S:RES*,P:Target*)~

  APPEND ~ACTION.IDS~
    ~240 ReallyForceSpellDeadRES(S:RES*,O:Target)~
  UNLESS ~240 ReallyForceSpellDeadRES(S:RES*,O:Target)~

  APPEND ~ACTION.IDS~
    ~318 ForceSpellRangeRES(S:RES*,O:Target)~
  UNLESS ~318 ForceSpellRangeRES(S:RES*,O:Target)~

  APPEND ~ACTION.IDS~
    ~319 ForceSpellPointRangeRES(S:RES*,P:Target)~
  UNLESS ~319 ForceSpellPointRangeRES(S:RES*,P:Target)~

  APPEND ~ACTION.IDS~
    ~337 ReallyForceSpellPointRES(S:RES*,P:Target*)~
  UNLESS ~337 ReallyForceSpellPointRES(S:RES*,P:Target*)~

  APPEND ~TRIGGER.IDS~
    ~0x4031 HaveSpellRES(S:Spell*)~
  UNLESS ~0x4031 HaveSpellRES(S:Spell*)~

  APPEND ~ACTION.IDS~
    ~335 SetTokenGlobal(S:GLOBAL*,S:Area*,S:Token*)~
  UNLESS ~335 SetTokenGlobal(S:GLOBAL*,S:Area*,S:Token*)~

  APPEND ~CLASS.IDS~
    ~202 LONG_BOW~
  UNLESS ~202 LONG_BOW~

  APPEND ~CLASS.IDS~
    ~202 MAGE_ALL~
  UNLESS ~202 MAGE_ALL~

  COPY_EXISTING ~race.ids~ ~override~
    REPLACE_TEXTUALLY ~^149 +SHADOW\b~ ~149 STATUE~
  BUT_ONLY_IF_IT_CHANGES

  CLEAR_IDS_MAP

END	// End of ALWAYS

README ~d0tweak/readme.html~

LANGUAGE ~English~
         ~lang/en~
         ~d0tweak/lang/en/-d0tweak.tra~
LANGUAGE ~French (by Raphaël Mizrahi, updated by Graoumf)~
         ~lang/fr~
         ~d0tweak/lang/en/-d0tweak.tra~
         ~d0tweak/lang/fr/-d0tweak.tra~
LANGUAGE ~Spanish (by Bhasbuto/Clan DLAN)~
         ~lang/sp~
         ~d0tweak/lang/en/-d0tweak.tra~
         ~d0tweak/lang/sp/-d0tweak.tra~
LANGUAGE ~Polish (by Damian Staniszewski)~
         ~lang/pl~
         ~d0tweak/lang/en/-d0tweak.tra~
         ~d0tweak/lang/pl/-d0tweak.tra~
LANGUAGE ~German (by Tanis Eichenblatt)~
         ~lang/de~
         ~d0tweak/lang/en/-d0tweak.tra~
         ~d0tweak/lang/de/-d0tweak.tra~
LANGUAGE ~Japanese (by Kuroukume Velve)~
         ~lang/jp~
         ~d0tweak/lang/en/-d0tweak.tra~
         ~d0tweak/lang/jp/-d0tweak.tra~


// ************************
// * Enhanced Goodberries *
// ************************

BEGIN @1

COPY ~d0tweak/goodberry/gberry2.itm~ ~override~
  SAY UNIDENTIFIED_DESC @2

COPY ~d0tweak/goodberry/gberry3.itm~ ~override~
  SAY UNIDENTIFIED_DESC @3

COPY ~d0tweak/goodberry/gberry4.itm~ ~override~
  SAY UNIDENTIFIED_DESC @4

COPY ~d0tweak/goodberry/gberry5.itm~ ~override~
  SAY UNIDENTIFIED_DESC @5

COPY ~d0tweak/goodberry/gberry6.itm~ ~override~
  SAY UNIDENTIFIED_DESC @6

COPY ~d0tweak/goodberry/gberry7.itm~ ~override~
  SAY UNIDENTIFIED_DESC @7

INCLUDE ~d0tweak/lib/macro_compat_divine_remix_spell.tph~

COPY ~d0tweak/goodberry/sppr207.spl~ ~override~
  SAY 0x50 @8
  LAUNCH_PATCH_MACRO ~compat_divine_remix_spell~


// ***************************************************
// * Corrected Summoned Demon Behaviour (DEPRECATED) *
// ***************************************************

BEGIN @9

DEPRECATED ~An updated version of this component is now incorporated into the G3 BG2 Fixpack at http://www.gibberlings3.net/.~


// *******************************************
// * Corrected Demon Animations (DEPRECATED) *
// *******************************************

BEGIN @10

DEPRECATED ~An updated version of this component is now incorporated into the BG2 Fixpack at http://www.gibberlings3.net/.~


// ******************************
// * Reintroduce Dimension Door *
// ******************************

BEGIN @14

REQUIRE_PREDICATE NOT GAME_IS ~tutu tutu_totsc~ ~Skipping Tutu-incompatible component.~

COPY ~d0tweak/ddoor/d0scrldd.itm~ ~override~

COPY_EXISTING ~scrolls.sto~ ~override~
  ADD_STORE_ITEM ~d0scrldd~ AFTER ~scrl5h~ #1 #0 #0 ~identified~ #1
BUT_ONLY_IF_IT_CHANGES

ACTION_IF FILE_EXISTS ~data/25Store.bif~
BEGIN
  COPY_EXISTING ~25spell.sto~ ~override~
                ~25spell2.sto~ ~override~
    ADD_STORE_ITEM ~d0scrldd~ BEFORE ~scrl5j~ #1 #0 #0 ~identified~ #4
  BUT_ONLY_IF_IT_CHANGES
END


// *******************************
// * Tweaked Weapon Enchantments *
// *******************************

BEGIN @16

REQUIRE_PREDICATE NOT GAME_IS ~tutu tutu_totsc~ ~Skipping Tutu-incompatible component.~

ACTION_IF GAME_IS ~tob bgt bg2ee~
BEGIN
  COPY_EXISTING ~deva.itm~ ~override~
                ~devaevil.itm~ ~override~
                ~devmon01.itm~ ~override~
		~planetar.itm~ ~override~
    WRITE_BYTE 0x60 4
    READ_BYTE 0x18 "flags"
    WRITE_BYTE 0x18 ("%flags%" BOR 0b01000000)
  BUT_ONLY_IF_IT_CHANGES
END

COPY_EXISTING ~sarevo.itm~ ~override~
  WRITE_BYTE 0x60 3
  READ_BYTE 0x18 "flags"
  WRITE_BYTE 0x18 ("%flags%" BOR 0b01000000)
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~bearspir.itm~ ~override~
              ~lionspir.itm~ ~override~
              ~snakspir.itm~ ~override~
              ~wolfspir.itm~ ~override~
  WRITE_BYTE 0x60 2
  READ_BYTE 0x18 "flags"
  WRITE_BYTE 0x18 ("%flags%" BOR 0b01000000)
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~arow10.itm~ ~override~
  WRITE_LONG 0x60 4
  READ_BYTE 0x18 "flags"
  WRITE_BYTE 0x18 ("%flags%" BOR 0b01000000)
BUT_ONLY_IF_IT_CHANGES


// *****************************
// * Additional Racial Enemies *
// *****************************

BEGIN @17

REQUIRE_PREDICATE NOT GAME_IS ~tutu tutu_totsc~ ~Skipping Tutu-incompatible component.~

COPY ~d0tweak/racial/haterace.2da~ ~override~
  REPLACE 99999 @18
  REPLACE 99998 @19
  REPLACE 99997 @20
  REPLACE 99996 @21
  REPLACE 99995 @22
  REPLACE 99994 @23
  REPLACE 99993 @24


// *************************
// * Summoning Cap Removal *
// *************************

BEGIN @25

REQUIRE_PREDICATE NOT GAME_IS ~tutu tutu_totsc~ ~Skipping Tutu-incompatible component.~

REQUIRE_PREDICATE NOT GAME_IS ~bgee bg2ee~ ~For the Enhanced versions, please use the Summoning Cap Removal component from G3 BG2 Tweak pack.~

INCLUDE ~d0tweak/lib/macro_remove_summoning_cap.tph~

LAUNCH_ACTION_MACRO ~REMOVE_SUMMONING_CAP~


// **********************
// * PnP Celestial Fury *
// **********************

BEGIN @26

REQUIRE_PREDICATE NOT GAME_IS ~tutu tutu_totsc~ ~Skipping Tutu-incompatible component.~

EXTEND_TOP ~baldur.bcs~ ~d0tweak/cfmod/cfmod.baf~
EXTEND_TOP ~baldur25.bcs~ ~d0tweak/cfmod/cfmod.baf~


// ****************************************
// * Ghrey's Holy Symbol Fix (DEPRECATED) *
// ****************************************

BEGIN @27

DEPRECATED ~An updated version of this component is now incorporated into the BG2 Fixpack at http://www.gibberlings3.net/.~

REQUIRE_PREDICATE NOT GAME_IS ~tutu tutu_totsc~ ~Skipping Tutu-incompatible component.~


// *****************
// * Kill Cespenar *
// *****************

BEGIN @32

REQUIRE_PREDICATE NOT GAME_IS ~tutu tutu_totsc~ ~Skipping Tutu-incompatible component.~

REQUIRE_PREDICATE GAME_IS ~tob bgt bg2ee~ ~Skipping ToB-specific component.~

COPY_EXISTING ~botsmith.cre~ ~override~
  WRITE_LONG 0x14 150
  WRITE_SHORT 0x24 50
  WRITE_SHORT 0x26 50
  WRITE_BYTE 0x59 100
  WRITE_BYTE 0x5a 100
  WRITE_BYTE 0x5b 75
  WRITE_BYTE 0x5c 0
  WRITE_BYTE 0x5d 35
  WRITE_BYTE 0x5e 100
  WRITE_BYTE 0x5f 100
  WRITE_BYTE 0x60 0
  WRITE_BYTE 0x61 0
  WRITE_BYTE 0x62 0
  WRITE_BYTE 0x63 0
  REPLACE_TEXTUALLY ~CLCK24~ ~CLCK25~
  REPLACE_TEXTUALLY ~INVULNER~ ~D0NOTHIN~


// *******************************************
// * Smarter Illusionary Clones (DEPRECATED) *
// *******************************************

BEGIN @33

DEPRECATED ~An updated version of this component is now incorporated into the AI Improvements component of Quest Pack at http://www.pocketplane.net/quest/.~


// **********************
// * Visual Ioun Stones *
// **********************

BEGIN @34

COPY_LARGE ~d0tweak/ioun/d0io1.bam~ ~override~
           ~d0tweak/ioun/d0io2.bam~ ~override~
           ~d0tweak/ioun/d0io3.bam~ ~override~
           ~d0tweak/ioun/d0io4.bam~ ~override~
           ~d0tweak/ioun/d0io5.bam~ ~override~
           ~d0tweak/ioun/d0io6.bam~ ~override~
           ~d0tweak/ioun/d0io7.bam~ ~override~
           ~d0tweak/ioun/d0io8.bam~ ~override~
           ~d0tweak/ioun/d0io9.bam~ ~override~

COPY ~d0tweak/ioun/sporbit.vvc~ ~override/d0io1.vvc~
  WRITE_ASCII 0x8 "D0IO1" #8
  WRITE_BYTE 0x1a 0x8

COPY ~d0tweak/ioun/sporbit.vvc~ ~override/d0io2.vvc~
  WRITE_ASCII 0x8 "D0IO2" #8

COPY ~d0tweak/ioun/sporbit.vvc~ ~override/d0io3.vvc~
  WRITE_ASCII 0x8 "D0IO3" #8

COPY ~d0tweak/ioun/sporbit.vvc~ ~override/d0io4.vvc~
  WRITE_ASCII 0x8 "D0IO4" #8

COPY ~d0tweak/ioun/sporbit.vvc~ ~override/d0io5.vvc~
  WRITE_ASCII 0x8 "D0IO5" #8

COPY ~d0tweak/ioun/sporbit.vvc~ ~override/d0io6.vvc~
  WRITE_ASCII 0x8 "D0IO6" #8

COPY ~d0tweak/ioun/sporbit.vvc~ ~override/d0io7.vvc~
  WRITE_ASCII 0x8 "D0IO7" #8

COPY ~d0tweak/ioun/sporbit.vvc~ ~override/d0io8.vvc~
  WRITE_ASCII 0x8 "D0IO8" #8

COPY ~d0tweak/ioun/sporbit.vvc~ ~override/d0io9.vvc~
  WRITE_ASCII 0x8 "D0IO9" #8


// Shadows of Amn ioun stones
ACTION_IF GAME_INCLUDES "soa"
BEGIN
  // Pearly White Ioun Stone
  COPY_EXISTING ~helm18.itm~ ~override~
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 215
      target		= 1
      timing		= 2
      parameter2		= 1
      STR_VAR
      resource		= ~d0io6~
    END

  // Dusty Rose Ioun Stone
  COPY_EXISTING ~helm19.itm~ ~override~
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 215
      target		= 1
      timing		= 2
      parameter2		= 1
      STR_VAR
      resource		= ~d0io7~
    END

  // Pale Green Ioun Stone
  COPY_EXISTING ~helm20.itm~ ~override~
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 215
      target		= 1
      timing		= 2
      parameter2		= 1
      STR_VAR
      resource		= ~d0io4~
    END
END

// Throne of Bhaal ioun stones
ACTION_IF GAME_INCLUDES "tob"
BEGIN
  // Malla's Soul Stone
  COPY_EXISTING ~amsoul01.itm~ ~override~
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 215
      target		= 1
      timing		= 2
      parameter2	= 1
      STR_VAR
      resource		= ~d0io2~
    END

  // Golden Ioun Stone
  // Bronze Ioun Stone
  COPY_EXISTING ~helm23.itm~ ~override~
                ~helm27.itm~ ~override~
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 215
      target		= 1
      timing		= 2
      parameter2	= 1
      STR_VAR
      resource		= ~d0io3~
    END

  // Obsidian Ioun Stone
  COPY_EXISTING ~helm24.itm~ ~override~
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 215
      target		= 1
      timing		= 2
      parameter2	= 1
      STR_VAR
      resource		= ~d0io1~
    END

  // Silver Ioun Stone
  COPY_EXISTING ~helm25.itm~ ~override~
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 215
      target		= 1
      timing		= 2
      parameter2	= 1
      STR_VAR
      resource		= ~d0io6~
    END

  // Lavender Ioun Stone
  COPY_EXISTING ~helm26.itm~ ~override~
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 215
      target		= 1
      timing		= 2
      parameter2	= 1
      STR_VAR
      resource		= ~d0io5~
    END

  // Wong Fei's Ioun Stone
  COPY_EXISTING ~helm34.itm~ ~override~
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 215
      target		= 1
      timing		= 2
      parameter2	= 1
      STR_VAR
      resource		= ~d0io4~
    END
END

// Ioun stones from Tweak Anthologies
// (formerly known as G3 BG2 Tweak Pack)
ACTION_IF FILE_EXISTS_IN_GAME ~cdioun1.itm~
BEGIN
  // Deep Purple Ioun Stone
  COPY_EXISTING ~cdioun1.itm~ ~override~
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 215
      target		= 1
      timing		= 2
      parameter2	= 1
      STR_VAR
      resource		= ~d0io8~
    END

  // Flickering White Ioun Stone
  COPY_EXISTING ~cdioun2.itm~ ~override~
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 215
      target		= 1
      timing		= 2
      parameter2	= 1
      STR_VAR
      resource		= ~d0io9~
    END
END

// Ioun stones from Rogue Revisions
ACTION_IF FILE_EXISTS_IN_GAME ~rr#hel01.itm~
BEGIN
  // Tri-souled Sapphire
  COPY_EXISTING ~rr#hel01.itm~ ~override~
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 215
      target		= 1
      timing		= 2
      parameter2	= 1
      STR_VAR
      resource		= ~d0io1~
    END

  // Fragment of Enlightenment
  COPY_EXISTING ~rr#hel04.itm~ ~override~
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 215
      target		= 1
      timing		= 2
      parameter2	= 1
      STR_VAR
      resource		= ~d0io6~
    END

  // Deep Red Ioun Stone
  COPY_EXISTING ~rr#hel05.itm~ ~override~
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 215
      target		= 1
      timing		= 2
      parameter2	= 1
      STR_VAR
      resource		= ~d0io5~
    END

  // Verduris' Luck Stone
  COPY_EXISTING ~rr#vluck.itm~ ~override~
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 215
      target		= 1
      timing		= 2
      parameter2	= 1
      STR_VAR
      resource		= ~d0io1~
    END
END

// Ioun Stones from The Sorcerer's Place Collection
ACTION_IF FILE_EXISTS_IN_GAME "cdivory.itm"
BEGIN
  // Ivory Ioun Stone
  COPY_EXISTING ~cdivory.itm~ ~override~
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 215
      target		= 1
      timing		= 2
      parameter2	= 1
      STR_VAR
      resource		= ~d0io9~
    END
END

// Ioun Stones from BG1 Quest Pack
ACTION_IF FILE_EXISTS_IN_GAME "c#q04001.itm"
BEGIN
  // Egg of the Frozen Spider
  COPY_EXISTING ~c#q04001.itm~ ~override~
  COPY_EXISTING ~c#q04002.itm~ ~override~
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 215
      target		= 1
      timing		= 2
      parameter2	= 1
      STR_VAR
      resource		= ~d0io6~
    END
END

// Ioun Stones from BG1 Romantic Encounters
ACTION_IF FILE_EXISTS_IN_GAME "c#lcthit.itm"
BEGIN
  // Thalantyr's Ioun Stone
  COPY_EXISTING ~c#lcthit.itm~ ~override~
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 215
      target		= 1
      timing		= 2
      parameter2	= 1
      STR_VAR
      resource		= ~d0io6~
    END
END


// **************************************
// * Dual Class Wild Mages (DEPRECATED) *
// **************************************

BEGIN @35

DEPRECATED ~An updated version of this component is now incorporated into the G3 Tweak Pack at http://www.gibberlings3.net/.~


// ***********************************************
// * Kensai Ryu's Restored Drow Innate Abilities *
// ***********************************************

BEGIN @36

REQUIRE_PREDICATE NOT GAME_IS ~tutu tutu_totsc~ ~Skipping Tutu-incompatible component.~

COPY_LARGE ~d0tweak/drow/krdl1.bam~ ~override~
           ~d0tweak/drow/krdl1.vvc~ ~override~
           ~d0tweak/drow/krglobd.bam~ ~override~
           ~d0tweak/drow/krglobd.vvc~ ~override~
           ~d0tweak/drow/krglobd2.bam~ ~override~
           ~d0tweak/drow/krglobd2.vvc~ ~override~

COPY ~d0tweak/drow/krin400.SPL~ ~override~
  SAY 0x8 @37
  SAY 0x50 @38

COPY ~d0tweak/drow/krin401.SPL~ ~override~
  SAY 0x8 @39
  SAY 0x50 @40

COPY ~d0tweak/drow/krin402.SPL~ ~override~
  SAY 0x8 @41
  SAY 0x50 @42
  SAY 0xfe @43
  SAY 0x12e @44

COPY ~d0tweak/drow/krin403.SPL~ ~override~
  SAY 0x8 @45
  SAY 0x50 @46

EXTEND_TOP ~wtasight.bcs~ ~d0tweak/drow/et_wta.baf~
EXTEND_TOP ~wtarsgt.bcs~ ~d0tweak/drow/et_wta.baf~
EXTEND_TOP ~wdasight.bcs~ ~d0tweak/drow/et_wta.baf~
EXTEND_TOP ~wdarsgt.bcs~ ~d0tweak/drow/et_wta.baf~


// ******************************************
// * Jaysyn's Displacer Beasts (DEPRECATED) *
// ******************************************

BEGIN @47

DEPRECATED ~An updated version of this component is now incorporated into the Miscellaneous Enhancements component of Quest Pack at http://www.pocketplane.net/quest/.~

REQUIRE_PREDICATE NOT GAME_IS ~tutu tutu_totsc~ ~Skipping Tutu-incompatible component.~


// **************************
// * Remove Quayle Portrait *
// **************************

BEGIN @50

REQUIRE_PREDICATE NOT GAME_IS ~tutu tutu_totsc~ ~Skipping Tutu-incompatible component.~

COPY_EXISTING ~quaylem.cre~ ~override~
  WRITE_ASCII ~0x34~ ~d0nothin~ #8
  WRITE_ASCII ~0x3c~ ~d0nothin~ #8


// **************
// * Dream Flag *
// **************

BEGIN @51

REQUIRE_PREDICATE NOT GAME_IS ~tutu tutu_totsc~ ~Skipping Tutu-incompatible component.~

COPY_EXISTING ~ar0011.are~ ~override~
              ~ar0013.are~ ~override~
              ~ar0084.are~ ~override~
              ~ar0085.are~ ~override~
              ~ar0086.are~ ~override~
  READ_BYTE 0x14 "flags"
  WRITE_BYTE 0x14 ("%flags%" BOR 0b00001000)
BUT_ONLY_IF_IT_CHANGES


// ***********************
// * Sensible Lore Table *
// ***********************

BEGIN @52

COPY ~d0tweak/lore~ ~override~


// *************************
// * Improved Backstabbing *
// *************************

BEGIN @53

COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~
PATCH_IF (SOURCE_SIZE > 0x71)
BEGIN // protects against invalid files
  READ_SHORT 0x1c "itemtype"
  SPRINT itemtypestring ~%itemtype%~

  PATCH_IF FILE_CONTAINS_EVALUATED (~d0tweak/backstab.txt~ ~^%itemtypestring% ~)
  BEGIN
    // read the penalty from the file
    INNER_PATCH_FILE ~d0tweak/backstab.txt~ BEGIN
      SET thisline = 999
      FOR (i = 0; thisline != itemtype; i+=1) BEGIN
        READ_2DA_ENTRY i 0 1 thisline
      END
      i -= 1
      READ_2DA_ENTRY i 1 1 penalty
    END // INNER_PATCH_FILE

    PATCH_IF itemtype = 20
    BEGIN
      READ_SHORT 0x31 "proficiency"
      PATCH_IF proficiency = 93
      BEGIN
        SET penalty = 3
      END
    END

    LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
      INT_VAR
      opcode		= 263
      target		= 1	// Self
      timing		= 2	// While equipped
      parameter1	= (0 - penalty)
    END
  END // PATCH_IF the itemtype is in the list
END // PATCH_IF SOURCE_SIZE is good
BUT_ONLY_IF_IT_CHANGES


// ******************************
// * NPCs/Summons Set Off Traps *
// ******************************

BEGIN @59
SUBCOMPONENT @55

REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~rr#stf01.bcs~
 ~This component is incompatible with the Shadow Thief Improvements component of Rogue Rebalancing.~

OUTER_SET "install_mode" = 1
INCLUDE ~d0tweak/lib/npctraps.tph~

BEGIN @60
SUBCOMPONENT @55
OUTER_SET "install_mode" = 2
INCLUDE ~d0tweak/lib/npctraps.tph~

BEGIN @61
SUBCOMPONENT @55
OUTER_SET "install_mode" = 3
INCLUDE ~d0tweak/lib/npctraps.tph~


// **************************************
// * Slower Walking Speeds (DEPRECATED) *
// **************************************

BEGIN @56

DEPRECATED ~An updated version of this component is now incorporated into the G3 Tweak Pack at http://www.gibberlings3.net/.~


// ******************************
// * BG1-Style Summoning Spells *
// ******************************

BEGIN @58

REQUIRE_PREDICATE NOT GAME_IS ~tutu tutu_totsc~ ~Skipping Tutu-incompatible component.~

REQUIRE_PREDICATE NOT GAME_IS ~bgee bg2ee~ ~Skipping Enhanced Edition-incompatible component.~

INCLUDE ~d0tweak/lib/macro_remove_summoning_cap.tph~
INCLUDE ~d0tweak/lib/macro_compat_divine_remix_spell.tph~

LAUNCH_ACTION_MACRO ~REMOVE_SUMMONING_CAP~

COPY ~d0tweak/summons/copy~ ~override~

COPY ~d0tweak/summons/spwi309.spl~ ~override~
  SAY 0x50 @2201

COPY ~d0tweak/summons/spwi407.spl~ ~override~
  SAY 0x50 @2203

COPY ~d0tweak/summons/spwi504.spl~ ~override~
  SAY 0x50 @2202

COPY ~d0tweak/summons/sppr402.spl~ ~override~
     ~d0tweak/summons/spra304.spl~ ~override~
  SAY 0x50 @2204
  LAUNCH_PATCH_MACRO ~compat_divine_remix_spell~

COPY ~d0tweak/summons/sppr501.spl~ ~override~
     ~d0tweak/summons/spra305.spl~ ~override~
  SAY 0x50 @2205
  LAUNCH_PATCH_MACRO ~compat_divine_remix_spell~

COPY ~d0tweak/summons/sppr602.spl~ ~override~
     ~d0tweak/summons/spra306.spl~ ~override~
  SAY 0x50 @2217
  LAUNCH_PATCH_MACRO ~compat_divine_remix_spell~

COPY ~d0tweak/summons/spwi501.spl~ ~override~
  SAY 0x50 @2206

COPY ~d0tweak/summons/sppr301.spl~ ~override~
  SAY 0x50 @2207
  LAUNCH_PATCH_MACRO ~compat_divine_remix_spell~

COPY_EXISTING ~scrl2d.itm~ ~override~
  SAY 0x54 @2206

COPY_EXISTING ~scrl2g.itm~ ~override~
  SAY 0x54 @2202

COPY_EXISTING ~scrl2a.itm~ ~override~
  SAY 0x54 @2203

COPY_EXISTING ~scrl1l.itm~ ~override~
  SAY 0x54 @2201

COPY_EXISTING ~SKELWASU.CRE~ ~override~
  WRITE_LONG 0x14 1150
  WRITE_BYTE 0x272 115

COPY ~d0tweak/summons/newcres~ ~override~

COPY ~d0tweak/summons/skeletons~ ~override~
  SAY NAME1 @2208
  SAY NAME2 @2208
  SAY BATTLE_CRY1 @2209 
  SAY BATTLE_CRY2 @2209 
  SAY BATTLE_CRY3 @2209 
  SAY BATTLE_CRY4 @2209 
  SAY BATTLE_CRY5 @2209
  SAY ATTACK1 @2210 
  SAY ATTACK2 @2211 
  SAY ATTACK3 @2212 
  SAY DAMAGE @2213 
  SAY DYING @2214 
  SAY SELECT_COMMON1 @2215 
  SAY SELECT_COMMON2 @2216


// **************************************
// * Continuous Imoen (NOT IMPLEMENTED) *
// **************************************

/* BEGIN @57

COPY_EXISTING ~imoen2j.dlg~ ~override~
  READ_LONG 0x8 "#states"
  PATCH_IF ("%#states%" > 0) BEGIN
    INNER_ACTION BEGIN
	  COPY_EXISTING ~d0tweak/imoen/contimoen.d~ ~d0tweak/imoen/contimoen.d~
	    FOR (int i = 0; i < "%#states%"; i++) BEGIN
		  REPLACE_TEXTUALLY ~ADD_STATE_TRIGGER IMOEN2J~ ~ADD_STATE_TRIGGER IMOEN2J %i%~
	    END
	  COMPILE ~d0tweak/imoen/contimoen.d~
    END
	DECOMPILE_AND_PATCH
	BEGIN
	  INNER_ACTION BEGIN
	    COPY_EXISTING ~imoenj.dlg~ ~override~
		  DECOMPILE_AND_PATCH
		  BEGIN
		    APPEND_FILE ~imoen2j.dlg.d~
			REPLACE_TEXTUALLY ~BEGIN IMOEN2J.*~ ~~
		  END
	    BUT_ONLY_IF_IT_CHANGES
	  END
	END
BUT_ONLY_IF_IT_CHANGES */


